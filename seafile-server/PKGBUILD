# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seafile-server
pkgver=3.0.2
pkgrel=3
pkgdesc="Next-generation open source cloud storage with advanced features on privacy protection and teamwork. (Server components, without seahub)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL3')
depends=('python2-mako' 'python2-webpy' 'python2-simplejson' 'python2-pillow' 'python2-chardet' 'python2-pip' 'python2-virtualenv' 'python2-flup' 'python2-six' 'libevhtp' 'fuse' 'seafile-shared>=2.2.0')
makedepends=('vala' 'intltool')
provides=()
conflicts=('django' 'django-rest-framework', 'seafile')
options=('!libtool' '!emptydirs')
install=seafile-server.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}-server.tar.gz"
        "seafile-admin.patch"
        "seafile-server.install"
        "seafile-server@.service"
        "0004-Add-autoconfiguration-for-libjansson.patch"
        "0001-Revert-server-put-pids-folder-out-of-seafile-data.patch")

prepare ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	patch -p1 -i ${srcdir}/seafile-admin.patch
	patch -p1 -i ${srcdir}/0004-Add-autoconfiguration-for-libjansson.patch
	patch -p1 -i ${srcdir}/0001-Revert-server-put-pids-folder-out-of-seafile-data.patch
}

build ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	./autogen.sh
	./configure --enable-server --enable-httpserver --disable-client --disable-python --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	# Install library and header files
	cd "${srcdir}/seafile-${pkgver}-server"
	make DESTDIR="${pkgdir}/" install

	# Remove files already installed by seafile-shared
	# to maintain compatibility
	rm -rf "${pkgdir}/usr/lib"
	rm -rf "${pkgdir}/usr/include"

	# Install upgrade scripts
	mkdir -p "${pkgdir}/usr/share/$pkgname/scripts"
	cp -r -p "${srcdir}/seafile-${pkgver}-server/scripts/upgrade" "${pkgdir}/usr/share/$pkgname/scripts/upgrade"
	rm -rf "${pkgdir}/usr/share/$pkgname/scripts/upgrade/win32"
	# Fix upgrade script's  python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/db_update_1.3_1.4.py"
	# Fix upgrade script's invald use of $INSTALLPATH to find seafile binaries
	sed -i 's|${INSTALLPATH}/seafile/bin|/usr/bin|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/upgrade_2.2_3.0.sh"

	# Workaround for strange behaviour in the Makefile, which
	# installs python bindings in the package root directory,
	# even though they were disabled
	rm -rf "${pkgdir}/seaserv"
	rm -rf "${pkgdir}/seafile"

	# Install license
	install -D -m644 ${srcdir}/seafile-${pkgver}-server/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt

	# Install systemd service
	install -D -m644 ${srcdir}/seafile-server@.service ${pkgdir}/usr/lib/systemd/system/seafile-server@.service
}
sha256sums=('63fa6b637ec0c029977ab9701bf8a21eca2bb4f903f51cd96dd031da8f633ac8'
            '4fae094df4b111a2609e0dc60646c7ac1579e29577bdfae29b305c9ea8fbfc0f'
            '0245cd46ebe9f05e29f02e106f38e8e3a6b49961f1e0fadeacd583ebb956869e'
            '943ed1bb7d4908b452ce8b7abff26e56f3f24582f6a2cf6e0dc4bf2bffd0a6d8'
            'd7866a07959ada02d71cbfb116d44f0674248e3eb9a6f69270cb525ea3d6d019'
            'b1748e826d8e7cccdd825b99864b74dfb5795312f8878d63e9a87105f4382e29')
